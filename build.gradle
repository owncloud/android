buildscript {
    ext {
        // SDK
        sdkCompileVersion = 31
        sdkMinVersion = 21
        sdkTargetVersion = 30

        // Android jetpack
        archLifecycleVersion = "2.1.0"
        roomVersion = "2.4.2"
        appCompat = "1.4.2"
        sqliteVersion = "2.2.0"
        lifecycleLiveData = "2.4.1"
        biometricVersion="1.1.0"
        androidX = '1.0.0'

        // Kotlin
        kotlinVersion = "1.6.21"
        coroutinesVersion = "1.6.2"

        // Koin
        koinVersion = '3.2.0'

        // Testing
        mockkVersion = '1.12.3'
        junitVersion = "4.13.2"
        extJunitVersion = '1.1.3'
        androidTestVersion = '1.4.0'
        espressoTestVersion = '3.4.0'
        fragmentTestVersion = "1.1.0"
        uiAutomatorTestVersion = "2.2.0"
        annotationTestVersion = "1.3.0"

        // Extensions
        ktxVersion = "1.1.0"

        // KTX extensions
        ktxCoreVersion = "1.7.0"
        ktxViewModelVersion = "2.4.0"
        ktxFragmentVersion = "1.4.1"
        ktxLiveData = "2.1.0"
    }

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.3.0"
    }
}

plugins {
    id "org.sonarqube" version "3.4.0.2513"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: 'org.sonarqube'
}

sonarqube {
    properties {
        property "sonar.projectKey", "owncloud_android"
        property "sonar.organization", "owncloud-1"
        property "sonar.projectVersion", getBranchName()
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

def getBranchName() {
    def name = "git rev-parse --abbrev-ref HEAD".execute()
    return name.text.toString().trim()
}
