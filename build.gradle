buildscript {
    ext {
        // SDK
        sdkCompileVersion = 33
        sdkMinVersion = 21
        sdkTargetVersion = 31

        // Android jetpack
        androidxArchCore = "2.1.0"
        androidxLifecycle = "2.5.1"
        androidxRoom = "2.4.3"
        androidxCore = "1.9.0"
        androidxFragment = "1.5.5"
        androidxAppcompat = "1.5.1"
        androidxLegacy = "1.0.0"

        // Kotlin
        orgJetbrainsKotlin = "1.7.20"
        orgJetbrainsKotlinx = "1.6.4"

        // Koin
        ioInsertKoin = "3.2.0"

        // Testing
        ioMockk = "1.13.2"
        junitVersion = "4.13.2"
        androidxTestExt = "1.1.4"
        androidxTest = "1.4.0"
        androidxTestEspresso = "3.5.0"
        androidxTestUiautomator = "2.2.0"
        androidxAnnotation = "1.3.0"
    }

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.3.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$orgJetbrainsKotlin"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.0.0"
    }
}

plugins {
    id "org.sonarqube" version "3.4.0.2513"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.sonarqube"
}

sonarqube {
    properties {
        property "sonar.projectKey", "owncloud_android"
        property "sonar.organization", "owncloud-1"
        property "sonar.projectVersion", getBranchName()
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

def getBranchName() {
    def name = "git rev-parse --abbrev-ref HEAD".execute()
    return name.text.toString().trim()
}
